---
title: "Tracking a drifting transmitter"
engine: julia
---

::: {.callout-note title="References"}
This tutorial is adapted from `Example A` presented in:<br>

- Mandar Chitre, "[Differentiable Ocean Acoustic Propagation Modeling](https://arl.nus.edu.sg/wp-content/uploads/2023/04/Chitre_Differentiable-Ocean-Acoustic-Propagation-Modeling.pdf)," in OCEANS 2023 IEEE/MTS ‚Äì Limerick, 5-8 June 2023.

A version of this example was also presented in the UComms 2020 webinar:<br>

- Mandar Chitre, "[Underwater Acoustics in the age of differentiable and probabilistic programming](https://www.facebook.com/watch/live/?v=2473971036238315)", UComms 2020 webinar, 3 December 2020.
:::

## Problem statement

Let us consider a scenario where a drifting probe acoustically transmits its sensor data periodically to a static receiver. The initial position of the sensor is perfectly known, and so is the environment. But the path of the sensor as it drifts is not known, but we'd like to get an estimate of it from the received acoustic signal. Due to the high data rate requirements, the receiver uses an equalization technique that requires an accurate estimate of the channel impulse response. We want to generate that using a propagation model and an accurate estimate of the location of the probe.

The environment is an iso-velocity channel with a constant depth of 20 m and known seabed parameters (density `œÅ` = 1500 kg/m¬≥, sound speed `c` = 1850 m/s, and attenuation `Œ¥` = 0.001). The probe uses a 1-2 kHz band for data transmission, and includes 101 pilots at 10 Hz spacing to aid with channel estimation. The transmission loss can be accurately measured at those pilot frequencies, since the transmit source level is assumed to be known, but phase information is assumed to be unavailable at each pilot.

```{julia}
#| echo: false
using UnderwaterAcoustics
using Plots
default(size=(600, 400))
plot(UnderwaterEnvironment(bathymetry = 20.0); xlims=(-10, 150))
scatter!([(0, -5)]; markersize=4, color=:blue)
scatter!([(100, -6)]; markersize=4, marker=:star, color=:red)
plot!([100, 140], [-6, -6.8])
annotate!(0, -4, text("RX", 10, :blue))
annotate!(100, -5, text("TX (1-2 kHz)", 10, :red))
annotate!(110, -7, text("drift path", 10, :black))
```

## Dataset

To illustrate the idea, we generate a 60-transmission dataset with a linearly drifting path for the transmitter. Since we have an range-independent iso-velocity environment, we can use the `PekerisRayTracer` (otherwise we could use the `RaySolver`):

```{julia}
#| output: false
using UnderwaterAcoustics
using DataFrames

function ùí¥((r, d, f, œÅ, c, Œ¥))
  env = UnderwaterEnvironment(
    bathymetry = 20.0,
    seabed = FluidBoundary(œÅ, c, Œ¥)
  )
  tx = AcousticSource(r, -d, f)
  rx = AcousticReceiver(0.0, -5.0)
  pm = PekerisRayTracer(env)
  transmission_loss(pm, tx, rx)
end

data = DataFrame([(
    range=100.0 + 0.5t,
    depth=6.0 + 0.01t,
    pilots=[ùí¥([100.0 + 0.5t, 6.0 + 0.01t, f, 1500.0, 1850.0, 0.001]) for f ‚àà 1000.0:10.0:2000.0]
  ) for t ‚àà 0.0:1.0:59.0])
```

## Gradient descent

In order to recover the drift path of the probe, we build a simple error model for the measured pilots. We initialize the model with the known starting location of the probe, and track the probe by minimizing the error through gradient descent.

Since our propagation model is differentiable, the gradient of the error can be automatically computed during the optimization using [`ForwardDiff.jl`](https://github.com/JuliaDiff/ForwardDiff.jl).

```{julia}
#| output: false
using ForwardDiff

# channel model for pilots
pilots(r, d) = [ùí¥([r, d, f, 1500.0, 1850.0, 0.001]) for f ‚àà 1000.0:10.0:2000.0]

# gradient descent optimization
function chparams(data)
  history = []
  Œ∏ = [100.0, 6.0]    # known initial location
  Œ∑ = [1e-4, 1e-6]    # learning rate
  for row ‚àà eachrow(data)
    err(Œ∏) = sum(abs2, pilots(Œ∏[1], Œ∏[2]) .- row.pilots)  # error model
    for i ‚àà 1:100      # iterations of simple gradient descent
      Œ∏ .-=  Œ∑ .* ForwardDiff.gradient(err, Œ∏)
    end
    push!(history, (range=Œ∏[1], depth=Œ∏[2]))
  end
  DataFrame(history)
end

p = chparams(data)
```

Now that we have a path estimate, let's check it against the ground truth:

```{julia}
using Plots

plot(data.range, -data.depth; linewidth=2, xlabel="Range (m)", ylabel="Depth (m)", label="Ground truth")
scatter!(p.range, -p.depth; markersize=2, label="Estimated")
```

We have a pretty good match!
